{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "76a383f8",
   "metadata": {},
   "source": [
    "Sampler"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "9839850b",
   "metadata": {},
   "outputs": [],
   "source": [
    "from sage.stats.distributions.discrete_gaussian_integer import DiscreteGaussianDistributionIntegerSampler\n",
    "from sage.stats.distributions.discrete_gaussian_lattice import DiscreteGaussianDistributionLatticeSampler\n",
    "import sage.matrix.matrix_integer_dense_hnf as HNF\n",
    "    \n",
    "def Norm_Q(Q, u):\n",
    "    return sqrt(InnerProduct_Q(Q, u, u))\n",
    "\n",
    "def Norm_eucl_2(v):\n",
    "    return v.dot_product(v)\n",
    "\n",
    "def InnerProduct_Q(Q, u, v):\n",
    "    return u * Q * v\n",
    "\n",
    "def Proj(Q, u, v):\n",
    "    #Projects v onto u using Q-inner product (Q-norm)\n",
    "    return (InnerProduct_Q(Q, u, v) / InnerProduct_Q(Q, u, u)) * u\n",
    "\n",
    "def GramSchmidt_Q(Q, B):\n",
    "    N = Q.dimensions()[0]\n",
    "    B_gram = [zero_vector(N) for _ in range(N)]\n",
    "    for i in range(N):\n",
    "        B_gram[i] = B[i]\n",
    "        for j in range(i):\n",
    "            B_gram[i] = B_gram[i] - Proj(Q, B_gram[j], B_gram[i])\n",
    "    \n",
    "    return B_gram\n",
    "\n",
    "def GramSchmidt(B):\n",
    "    #Sagemath GSO (for Q-forms sampling)\n",
    "    return B.gram_schmidt(orthonormal=False)[0]\n",
    "\n",
    "def SortByColumnNorm(B):\n",
    "    N = B.dimensions()[0]\n",
    "    columns = B.columns()\n",
    "    columns.sort(key=lambda v : Norm_eucl_2(v))\n",
    "    return block_matrix([[matrix(column).transpose() for column in columns]])\n",
    "\n",
    "def Rho(x, s, c):\n",
    "    return exp(-pi * Norm_eucl_2(x - c)/ s^2)\n",
    "\n",
    "\n",
    "def InsertColumn(A, c, v):\n",
    "    N, M = A.dimensions()\n",
    "    for i in range(N):\n",
    "        A[i,c] = v[i]\n",
    "\n",
    "\n",
    "'''\n",
    "Sage samples with exp(-x**2/(2*s**2)).\n",
    "Need exp(-pi*||x||**2/s**2) -> rescale the sampling width by 1/sqrt(2*pi).\n",
    "'''\n",
    "def SampleZ(s, c):\n",
    "    D = DiscreteGaussianDistributionIntegerSampler(sigma=s/sqrt(2*pi), c=c)\n",
    "    return D()\n",
    "\n",
    "\n",
    "#Q-norm Gaussian sampler\n",
    "def SampleD_c(Q, B, B_, s, c):\n",
    "    N = Q.dimensions()[0]\n",
    "    \n",
    "    vi = zero_vector(ZZ, N)\n",
    "    ci = vector(c)\n",
    "    \n",
    "    for i in range(N-1, -1, -1):\n",
    "        bi_ = B_[i]\n",
    "        c_ = InnerProduct_Q(Q, ci, bi_) / InnerProduct_Q(Q, bi_, bi_)\n",
    "        s_ = s / Norm_Q(Q, bi_)\n",
    "        \n",
    "        zi = SampleZ(s_, c_)\n",
    "        bi = B[i]\n",
    "        ci = ci - zi * bi\n",
    "        vi = vi + zi * bi\n",
    "\n",
    "    return vi\n",
    "\n",
    "\n",
    "#Q-forms sampling\n",
    "def FirstN(V):\n",
    "    \"\"\"\n",
    "    First N linearly independent of vectors |v1| <= |v2| <= ... <= |vN| (SortByColumnNorm).\n",
    "    \"\"\"\n",
    "    N, M = V.dimensions()\n",
    "    V_ = matrix(QQ, N, 0)\n",
    "\n",
    "    for column in V.columns():\n",
    "        aux = block_matrix([[V_, matrix(column).transpose()]])\n",
    "\n",
    "        if aux.rank() > V_.rank():\n",
    "            V_ = aux\n",
    "\n",
    "        if V_.rank() == N:\n",
    "            return V_\n",
    "    \n",
    "    \n",
    "def Extract(B, V):\n",
    "    N, M = V.dimensions()\n",
    "\n",
    "    Y = matrix(ZZ, B.inverse() * V)\n",
    "    H, U_ = HNF.hnf_with_transformation(Y) #H = Y*U^(-1)\n",
    "    \n",
    "    print('HNF DONE')\n",
    "\n",
    "    U = matrix(ZZ, U_.inverse())\n",
    "    R = B * U\n",
    "    \n",
    "    print('INV+MULT DONE')\n",
    "    #assert V == R * H\n",
    "    V_ = GramSchmidt(V)\n",
    "    \n",
    "    print('GramSchmidt DONE -> slow')\n",
    "\n",
    "    for k in range(N):\n",
    "        v = V.column(k)\n",
    "        r = R.column(k)\n",
    "\n",
    "        if Norm_eucl_2(r) <= max((k+1)/4, 1) * Norm_eucl_2(v):\n",
    "            continue\n",
    "\n",
    "        R_ = GramSchmidt(R)\n",
    "\n",
    "        r_ = R_.column(k)\n",
    "        v_ = V_.column(k)\n",
    "\n",
    "        if r_ == v_ or r_ == -v_:\n",
    "            InsertColumn(R, k, v)\n",
    "        else:\n",
    "            x = r - r_\n",
    "            BR = block_matrix([[matrix(R.column(i)).transpose() for i in range(k)]])\n",
    "            v_pr = NearestPlane(BR, x)\n",
    "            InsertColumn(R, k, r - v_pr)\n",
    "\n",
    "    # R is modified -> recompute U such that R = B * U\n",
    "    U = matrix(ZZ, N, N, B.inverse() * R)\n",
    "    return (R.transpose() * R, U)\n",
    "\n",
    "\n",
    "\n",
    "def SampleQuadraticForm(B, s):\n",
    "    \"\"\"\n",
    "    Alg 8 from our file.\n",
    "    \"\"\"\n",
    "    print(\"Sampling U\")\n",
    "    N = B.dimensions()[0]\n",
    "    C = 1 - (1 + exp(-pi))^(-1)\n",
    "    m = ceil(2*N / C)\n",
    "    c = zero_vector(ZZ, N)\n",
    "    \n",
    "    #Decrease m while testing\n",
    "    #m = m//2\n",
    "\n",
    "    D = DiscreteGaussianDistributionLatticeSampler(B.transpose(), s/sqrt(2*3.14), zero_vector(N))\n",
    "    V = matrix(ZZ, m, N, [D() for _ in range(m)]).transpose()\n",
    "\n",
    "    if V.rank() < N:\n",
    "        return SampleQuadraticForm(B, s)\n",
    "    \n",
    "    print('SAMPLING DONE -> slow')\n",
    "    V = FirstN(V)\n",
    "    print('SIMPLIFY DONE')\n",
    "    V = SortByColumnNorm(V)\n",
    "    print('SORT DONE')\n",
    "    R, U = Extract(B, V)\n",
    "    return (R, U, V)\n",
    "    \n",
    "\n",
    "\n",
    "#BW construction\n",
    "def BW_n(n):\n",
    "    phi = 1 + i \n",
    "    A = matrix([[1, 0], [1, phi]])\n",
    "    #print(A)\n",
    "    result = A\n",
    "    for _ in range(n - 1):\n",
    "        result = result.tensor_product(A)   \n",
    "    return result\n",
    "\n",
    "\n",
    "def complex_to_2x2_block(a_plus_ib):\n",
    "    a = a_plus_ib.real()\n",
    "    b = a_plus_ib.imag()\n",
    "    return matrix([[a, b], [-b, a]])\n",
    "\n",
    "def matrix_integer(M):\n",
    "    n = M.nrows()\n",
    "    new_size = 2 * n\n",
    "    new_matrix = matrix(QQ, new_size, new_size, 0)\n",
    "    \n",
    "    for i in range(n):\n",
    "        for j in range(n):\n",
    "            block = complex_to_2x2_block(M[i, j])\n",
    "            for bi in range(2):\n",
    "                for bj in range(2):\n",
    "                    new_matrix[2*i + bi, 2*j + bj] = block[bi, bj]\n",
    "    \n",
    "    return new_matrix"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "8a0eabe1",
   "metadata": {},
   "source": [
    "Q-norm "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "22668f79",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "(64, 64)\n"
     ]
    }
   ],
   "source": [
    "k = 5\n",
    "n = 2^(k+1)\n",
    "\n",
    "BW_complex = BW_n(k)\n",
    "\n",
    "BW = matrix_integer(BW_complex)\n",
    "\n",
    "Q = BW.T*BW\n",
    "\n",
    "B = identity_matrix(n) #Z^n\n",
    "\n",
    "B_gram = GramSchmidt_Q(Q, B)# > n^4 multiplications (compute -> hard code?)\n",
    "\n",
    "print(matrix(B_gram).dimensions())\n",
    "\n",
    "#print(list(B))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "f47da1f5",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "(2670, 3993, 5817, 7649, 7432, 3067, 4642, 6530, 5733, 61, 7593, 3588, 6072, 8212, 7516, 8493, 5763, 6439, 6686, 1302, 632, 65, 6370, 7191, 3931, 5727, 4332, 2219, 2782, 774, 1347, 9108, 3889, 866, 8417, 7742, 5074, 9336, 3244, 4327, 207, 3055, 1227, 9832, 1393, 4028, 3286, 3785, 67, 5556, 8519, 4410, 5989, 6674, 555, 1917, 380, 6035, 3274, 9258, 5158, 9386, 7422, 2098)\n",
      "9.42515299934885\n",
      "9.42515299934885\n",
      "24.0000000000000\n"
     ]
    }
   ],
   "source": [
    "s = 3\n",
    "\n",
    "q = 9847\n",
    "\n",
    "t = vector([randint(1,q) for _ in range(n)])/q\n",
    "t = t + vector([randint(1,q) for _ in range(n)])\n",
    "\n",
    "sig = SampleD_c(Q, B, B_gram, s, t)  \n",
    "\n",
    "print(sig)\n",
    "\n",
    "print(Norm_Q(Q,t-sig).n())\n",
    "\n",
    "print((BW*(t-sig)).norm().n())\n",
    "\n",
    "print((s*sqrt(n)).n())"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b1c94d8d",
   "metadata": {},
   "outputs": [],
   "source": []
  },
  {
   "cell_type": "markdown",
   "id": "0768edb5",
   "metadata": {},
   "source": [
    "Euclidean norm"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "fa34d22e",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Sampling U\n",
      "SAMPLING DONE -> slow\n",
      "SIMPLIFY DONE\n",
      "SORT DONE\n",
      "HNF DONE\n",
      "INV+MULT DONE\n",
      "GramSchmidt DONE -> slow\n"
     ]
    }
   ],
   "source": [
    "#BW construction\n",
    "k = 5\n",
    "n = 2^(k+1)\n",
    "BW = matrix_integer(BW_n(k))\n",
    "\n",
    "#Quadratic forms sampling\n",
    "P, U, S = SampleQuadraticForm(BW, 30)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "5fd69b2b",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "9.10258994340492 Sig P-norm\n",
      "1.41947918053786e86 Sig eucl norm\n",
      "9.10258994340492 P-norm of signature\n",
      "9.10258994340492 S-norm of signature\n",
      "\n",
      "Restriction on norm\n",
      "24.0000000000000\n",
      "\n",
      "9.10258994340492\n"
     ]
    }
   ],
   "source": [
    "q = 9847\n",
    "\n",
    "#Target \n",
    "t = vector([randint(1,(q-1)) for _ in range(n)])/q + vector([randint(1,(q-1)) for _ in range(n)])\n",
    "\n",
    "#Gaussian distribution params\n",
    "s = 3\n",
    "cent = BW*U*t #new center\n",
    "\n",
    "D = DiscreteGaussianDistributionLatticeSampler(BW, sigma = s/sqrt(2*3.14), c = cent)\n",
    "\n",
    "sig_ = D()\n",
    "\n",
    "\n",
    "sig = U.inverse()*BW.inverse()*sig_\n",
    "\n",
    "#print(\"sig = \", sig , end='\\n\\n')\n",
    "\n",
    "print(Norm_Q(P, sig-t).n(),'Sig P-norm') #Condition to check in ver function\n",
    "print((t-sig).norm().n(),'Sig eucl norm')\n",
    "\n",
    "#Check\n",
    "print((BW*U*sig-BW*U*t).norm().n(), 'P-norm of signature') #P-norm of signature\n",
    "print((sig_-cent).norm().n(), 'S-norm of signature',end=\"\\n\\n\") #S-norm of signature, cent \\in sk\n",
    "\n",
    "\n",
    "print(\"Restriction on norm\")\n",
    "print((s*sqrt(n).n()), end=\"\\n\\n\") #Restriction on norm\n",
    "\n",
    "\n",
    "print((BW*U*(sig-t)).norm().n())# now (sig-t) \\in Q^n (need Z^n?), norm of BW'*(sig-t) is small (BW' = BW*U) \n",
    "#Signature forgery <-> find short vector in BW*U with center t <-> hard as long as U \\in sk (BW*U is unknown)\n",
    "\n",
    "#print((BW*(sig_-cent)).norm().n())"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "621c2180",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 10.2",
   "language": "sage",
   "name": "sagemath"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.10.12"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
